(ns behave.worksheet.subs
  (:require [clojure.string                :as str]
            [clojure.set                   :as set]
            [austinbirch.reactive-entity   :as re]
            [datascript.core               :as d]
            [re-posh.core                  :as rp]
            [re-frame.core                 :as rf]
            [behave.store                  :as s]
            [behave.vms.store              :refer [vms-conn]]
            [behave.schema.core            :refer [rules]]
            [behave.map-utils.interface    :refer [index-by]]
            [behave.number-utils.interface :refer [parse-float to-precision]]
            [behave.string-utils.interface :refer [->str ->kebab]]))

;; Helpers
(defn make-tree
  [xs]
  (into {} (map (fn [x] [(butlast x) [(last x)]]) xs)))

(defn input-tree-to-vec
  [[path leaf]]
  (let [input-vec (vec (concat (vec path) leaf))]
    (if (= (count input-vec) 4)
      (conj input-vec :none)
      input-vec)))

(defn re-entity-from-uuid [bp-uuid]
  (re/entity [:bp/uuid bp-uuid]))

(defn re-entity-from-eid [eid]
  (re/entity eid))

;; Retrieve all worksheet UUID's
(rp/reg-sub
 :worksheet/all
 (fn [_ _]
   {:type  :query
    :query '[:find  ?created ?uuid
             :where [?ws :worksheet/uuid ?uuid]
             [?ws :worksheet/created ?created]]}))

;; Retrieve latest worksheet UUID
(rf/reg-sub
 :worksheet/latest
 (fn [_]
   (rf/subscribe [:worksheet/all]))
 (fn [all-worksheets [_]]
   (last (last (sort-by first all-worksheets)))))

;; Retrieve worksheet as reactive entity
(rf/reg-sub
 :worksheet
 (fn [_ [_ ws-uuid]]
   (when-let [eid (d/entid @@s/conn [:worksheet/uuid ws-uuid])]
     (let [worksheet (re/entity eid)]
       (when (re/exists? worksheet)
         worksheet)))))

;; Retrieve worksheet as entity
(rf/reg-sub
 :worksheet-entity
 (fn [_ [_ ws-uuid]]
   (d/entity @@s/conn [:worksheet/uuid ws-uuid])))

(rf/reg-sub
 :worksheet/modules
 (fn [[_ ws-uuid]]
   (rf/subscribe [:worksheet ws-uuid]))

 (fn [worksheet _]
   (map #(deref (rf/subscribe [:wizard/*module (name %)]))
        (:worksheet/modules worksheet))))

;; Get state of a particular output
(rf/reg-sub
 :worksheet/output-enabled?
 (fn [[_ ws-uuid _variable-uuid]]
   (rf/subscribe [:worksheet ws-uuid]))

 (fn [worksheet [_ _ws-uuid variable-uuid]]
   (->> worksheet
        (:worksheet/outputs)
        (filter (fn matching-uuid [output]
                  (= (:output/group-variable-uuid output) variable-uuid)))
        (first)
        (:output/enabled?))))

;; Get the value of a particular input
(rp/reg-sub
 :worksheet/input-value
 (fn [_ [_ ws-uuid group-uuid repeat-id group-variable-uuid]]
   {:type      :query
    :query     '[:find  ?value .
                 :in    $ ?ws-uuid ?group-uuid ?repeat-id ?group-var-uuid
                 :where
                 [?w :worksheet/uuid ?ws-uuid]
                 [?w :worksheet/input-groups ?g]
                 [?g :input-group/group-uuid ?group-uuid]
                 [?g :input-group/repeat-id ?repeat-id]
                 [?g :input-group/inputs ?i]
                 [?i :input/group-variable-uuid ?group-var-uuid]
                 [?i :input/value ?value]]
    :variables [ws-uuid group-uuid repeat-id group-variable-uuid]}))

;; Get the units for a particular input
(rp/reg-sub
 :worksheet/input-units
 (fn [_ [_ ws-uuid group-uuid repeat-id group-variable-uuid]]
   {:type      :query
    :query     '[:find  ?unit-uuid .
                 :in    $ ?ws-uuid ?group-uuid ?repeat-id ?group-var-uuid
                 :where
                 [?w :worksheet/uuid ?ws-uuid]
                 [?w :worksheet/input-groups ?g]
                 [?g :input-group/group-uuid ?group-uuid]
                 [?g :input-group/repeat-id ?repeat-id]
                 [?g :input-group/inputs ?i]
                 [?i :input/group-variable-uuid ?group-var-uuid]
                 [?i :input/units ?unit-uuid]]
    :variables [ws-uuid group-uuid repeat-id group-variable-uuid]}))

;; Find groups matching a group-uuid
(rp/reg-sub
 :worksheet/repeat-groups
 (fn [_ [_ ws-uuid group-uuid]]
   {:type      :query
    :query     '[:find  [?g ...]
                 :in    $ ?ws-uuid ?group-uuid
                 :where
                 [?w :worksheet/uuid ?ws-uuid]
                 [?w :worksheet/input-groups ?g]
                 [?g :input-group/group-uuid ?group-uuid]]
    :variables [ws-uuid group-uuid]}))

;; Find inputs for a given group-uuid and repeat-id
(rp/reg-sub
 :worksheet/input-ids
 (fn [_ [_ ws-uuid group-uuid repeat-id]]
   {:type      :query
    :query     '[:find [?i ...]
                 :in  $ ?ws-uuid ?group-uuid ?repeat-id
                 :where
                 [?w :worksheet/uuid ?ws-uuid]
                 [?w :worksheet/input-groups ?g]
                 [?g :input-group/group-uuid ?group-uuid]
                 [?g :input-group/repeat-id ?repeat-id]
                 [?g :input-group/inputs ?i]]
    :variables [ws-uuid group-uuid repeat-id]}))

(rp/reg-sub
 :worksheet/group-repeat-ids
 (fn [_ [_ ws-uuid group-uuid]]
   {:type      :query
    :query     '[:find  [?rid ...]
                 :in    $ ?ws-uuid ?group-uuid
                 :where
                 [?w :worksheet/uuid ?ws-uuid]
                 [?w :worksheet/input-groups ?ig]
                 [?ig :input-group/group-uuid ?group-uuid]
                 [?ig :input-group/repeat-id ?rid]]
    :variables [ws-uuid group-uuid]}))

;; Find inputs for a given group-uuid and repeat-id
(rp/reg-sub
 :worksheet/input-ids
 (fn [_ [_ ws-uuid group-uuid repeat-id]]
   {:type      :query
    :query     '[:find [?i ...]
                 :in  $ ?ws-uuid ?group-uuid ?repeat-id
                 :where
                 [?w :worksheet/uuid ?ws-uuid]
                 [?w :worksheet/input-groups ?g]
                 [?g :input-group/group-uuid ?group-uuid]
                 [?g :input-group/repeat-id ?repeat-id]
                 [?g :input-group/inputs ?i]]
    :variables [ws-uuid group-uuid repeat-id]}))

(rp/reg-sub
 :worksheet/group-repeat-ids
 (fn [_ [_ ws-uuid group-uuid]]
   {:type      :query
    :query     '[:find  [?rid ...]
                 :in    $ ?ws-uuid ?group-uuid
                 :where
                 [?w :worksheet/uuid ?ws-uuid]
                 [?w :worksheet/input-groups ?ig]
                 [?ig :input-group/group-uuid ?group-uuid]
                 [?ig :input-group/repeat-id ?rid]]
    :variables [ws-uuid group-uuid]}))

(rf/reg-sub
 :worksheet/all-inputs-vector
 (fn [_ [_ ws-uuid]]
   (let [inputs @(rf/subscribe [:query
                                '[:find  ?group-uuid ?repeat-id ?group-var-uuid ?value
                                  :in    $ ?ws-uuid
                                  :where
                                  [?w :worksheet/uuid ?ws-uuid]
                                  [?w :worksheet/input-groups ?g]
                                  [?g :input-group/group-uuid ?group-uuid]
                                  [?g :input-group/repeat-id ?repeat-id]
                                  [?g :input-group/inputs ?i]
                                  [?i :input/group-variable-uuid ?group-var-uuid]
                                  [?i :input/value ?value]]
                                [ws-uuid]])]
     (into [] inputs))))

(rf/reg-sub
 :worksheet/all-native-units
 (fn [_ [_ ws-uuid]]
   (into []
         (d/q '[:find  ?group-uuid ?repeat-id ?gv-uuid ?unit-uuid
                :in    $ $ws % ?ws-uuid
                :where
                [$ws ?w :worksheet/uuid ?ws-uuid]
                [$ws ?w :worksheet/input-groups ?g]
                [$ws ?g :input-group/group-uuid ?group-uuid]
                [$ws ?g :input-group/repeat-id ?repeat-id]
                [$ws ?g :input-group/inputs ?i]
                [$ws ?i :input/group-variable-uuid ?gv-uuid]
                (lookup ?gv-uuid ?gv)
                (group-variable _ ?gv ?v)
                [?v :variable/kind :continuous]
                [?v :variable/native-unit-uuid ?unit-uuid]]
              @@vms-conn @@s/conn rules ws-uuid))))

(rf/reg-sub
 :worksheet/all-cached-units
 (fn [_]
   (rf/subscribe [:settings/local-storage-units]))

 (fn [units-settings [_ ws-uuid]]
   (into []
         (comp (filter (fn [[_ _ _ v-uuid]] (contains? units-settings v-uuid)))
               (map (fn [[group-uuid repeat-uuid gv-uuid v-uuid]]
                      [group-uuid repeat-uuid gv-uuid (get-in units-settings [v-uuid :unit-uuid])])))
         (d/q '[:find  ?group-uuid ?repeat-id ?gv-uuid ?v-uuid
                :in    $ $ws % ?ws-uuid
                :where
                [$ws ?w :worksheet/uuid ?ws-uuid]
                [$ws ?w :worksheet/input-groups ?g]
                [$ws ?g :input-group/group-uuid ?group-uuid]
                [$ws ?g :input-group/repeat-id ?repeat-id]
                [$ws ?g :input-group/inputs ?i]
                [$ws ?i :input/group-variable-uuid ?gv-uuid]
                (lookup ?gv-uuid ?gv)
                (group-variable _ ?gv ?v)
                [?v :variable/kind :continuous]
                [?v :bp/uuid ?v-uuid]]
              @@vms-conn @@s/conn rules ws-uuid))))

(rf/reg-sub
 :worksheet/all-custom-units
 (fn [_ [_ ws-uuid]]
   (into []
         (d/q '[:find  ?group-uuid ?repeat-id ?gv-uuid ?unit-uuid
                :in    $ $ws % ?ws-uuid
                :where
                [$ws ?w :worksheet/uuid ?ws-uuid]
                [$ws ?w :worksheet/input-groups ?g]
                [$ws ?g :input-group/group-uuid ?group-uuid]
                [$ws ?g :input-group/repeat-id ?repeat-id]
                [$ws ?g :input-group/inputs ?i]
                [$ws ?i :input/group-variable-uuid ?gv-uuid]
                (lookup ?gv-uuid ?gv)
                (group-variable _ ?gv ?v)
                [$ws ?i :input/units ?unit-uuid]]
              @@vms-conn @@s/conn rules ws-uuid))))

(rf/reg-sub
 :worksheet/all-inputs+units-vector
 (fn [[_ ws-uuid]]
   [(rf/subscribe [:worksheet/all-inputs-vector ws-uuid])
    (rf/subscribe [:worksheet/all-native-units ws-uuid])
    (rf/subscribe [:worksheet/all-custom-units ws-uuid])
    (rf/subscribe [:worksheet/all-cached-units ws-uuid])])
 (fn [sub-results]
   (let [[inputs native-units custom-units cached-units] (map make-tree sub-results)]
     (mapv input-tree-to-vec (merge-with (comp vec concat)
                                         inputs
                                         (merge native-units
                                                cached-units
                                                custom-units))))))

(rf/reg-sub
 :worksheet/all-inputs
 (fn [_ [_ ws-uuid]]
   (let [inputs @(rf/subscribe [:query
                                '[:find  ?group-uuid ?repeat-id ?group-var-uuid ?value
                                  :in    $ ?ws-uuid
                                  :where [?w :worksheet/uuid ?ws-uuid]
                                  [?w :worksheet/input-groups ?g]
                                  [?g :input-group/group-uuid ?group-uuid]
                                  [?g :input-group/repeat-id ?repeat-id]
                                  [?g :input-group/inputs ?i]
                                  [?i :input/group-variable-uuid ?group-var-uuid]
                                  [?i :input/value ?value]]
                                [ws-uuid]])]
     (reduce (fn [acc [group-uuid repeat-id group-var-uuid value]]
               (assoc-in acc [group-uuid repeat-id group-var-uuid] value))
             {}
             inputs))))

(rf/reg-sub
 :worksheet/all-input-values
 (fn [_ [_ ws-uuid]]
   @(rf/subscribe [:query
                   '[:find [?value ...]
                     :in $ ?ws-uuid
                     :where
                     [?w :worksheet/uuid ?ws-uuid]
                     [?w :worksheet/input-groups ?g]
                     [?g :input-group/inputs ?i]
                     [?i :input/value ?value]]
                   [ws-uuid]])))

(rf/reg-sub
 :worksheet/input-id+value
 (fn [_ [_ ws-uuid]]
   @(rf/subscribe [:query
                   '[:find ?group-var-uuid ?value
                     :in $ ?ws-uuid
                     :where
                     [?w :worksheet/uuid ?ws-uuid]
                     [?w :worksheet/input-groups ?g]
                     [?g :input-group/inputs ?i]
                     [?i :input/group-variable-uuid ?group-var-uuid]
                     [?i :input/value ?value]]
                   [ws-uuid]])))

(rf/reg-sub
 :worksheet/multi-value-input-uuid+value
 (fn [[_ ws-uuid]]
   (rf/subscribe [:worksheet/input-id+value ws-uuid]))

 (fn [inputs _query]
   (->> inputs
        (filter (fn multiple-values? [[_uuid value]]
                  (> (count (str/split value #",|\s"))
                     1))))))

(rf/reg-sub
 :worksheet/multi-value-input-uuids
 (fn [[_ ws-uuid]]
   [(rf/subscribe [:worksheet/multi-value-input-uuid+value ws-uuid])
    (rf/subscribe [:vms/group-variable-order])])

 (fn [[inputs gv-order] _query]
   (->> inputs
        (map first)
        (sort-by #(.indexOf gv-order %)))))

(rp/reg-sub
 :worksheet/all-output-uuids
 (fn [_ [_ ws-uuid]]
   {:type      :query
    :query     '[:find  [?uuid ...]
                 :in    $ ?ws-uuid
                 :where
                 [?w :worksheet/uuid ?ws-uuid]
                 [?w :worksheet/outputs ?o]
                 [?o :output/group-variable-uuid ?uuid]
                 [?o :output/enabled? true]]
    :variables [ws-uuid]}))

(rp/reg-sub
 :worksheet/get-table-settings-attr
 (fn [_ [_ ws-uuid attr]]
   {:type      :query
    :query     '[:find  [?value ...]
                 :in    $ ?ws-uuid ?attr
                 :where
                 [?w :worksheet/uuid ?ws-uuid]
                 [?w :worksheet/table-settings ?t]
                 [?t ?attr ?value]]
    :variables [ws-uuid attr]}))

(rp/reg-sub
 :worksheet/get-graph-settings-attr
 (fn [_ [_ ws-uuid attr]]
   {:type      :query
    :query     '[:find  [?value ...]
                 :in    $ ?ws-uuid ?attr
                 :where
                 [?w :worksheet/uuid ?ws-uuid]
                 [?w :worksheet/graph-settings ?g]
                 [?g ?attr ?value]]
    :variables [ws-uuid attr]}))

(rp/reg-sub
 :worksheet/graph-settings-y-axis-limits
 (fn [_ [_ ws-uuid]]
   {:type  :query
    :query '[:find ?group-var-uuid ?min ?max
             :in   $ ?ws-uuid
             :where
             [?w :worksheet/uuid ?ws-uuid]
             [?w :worksheet/graph-settings ?g]
             [?g :graph-settings/y-axis-limits ?y]
             [?y :y-axis-limit/group-variable-uuid ?group-var-uuid]
             [?y :y-axis-limit/min ?min]
             [?y :y-axis-limit/max ?max]
             [?w :worksheet/outputs ?o]
             [?o :output/group-variable-uuid ?group-var-uuid]
             [?o :output/enabled? true]]
    :variables [ws-uuid]}))

(rp/reg-sub
 :worksheet/table-settings-filters
 (fn [_ [_ ws-uuid]]
   {:type      :query
    :query     '[:find ?group-var-uuid ?min ?max ?enabled
                 :in   $ ?ws-uuid
                 :where
                 [?w :worksheet/uuid ?ws-uuid]
                 [?w :worksheet/table-settings ?ts]
                 [?ts :table-settings/filters ?tf]
                 [?tf :table-filter/group-variable-uuid ?group-var-uuid]
                 [?tf :table-filter/min ?min]
                 [?tf :table-filter/max ?max]
                 [?tf :table-filter/enabled? ?enabled]
                 [?w :worksheet/outputs ?o]
                 [?o :output/group-variable-uuid ?group-var-uuid]
                 [?o :output/enabled? true]]
    :variables [ws-uuid]}))

;; Results Table formatters

(defn ^:private create-formatter [variable]
  (condp = (:variable/kind variable)

    :continuous
    (let [*cached-decimals   (rf/subscribe [:settings/cached-decimal (:bp/uuid variable)])
          significant-digits (or @*cached-decimals (:variable/native-decimals variable))]
      (fn continuous-fmt [value]
        (-> value
            (parse-float)
            (to-precision significant-digits))))

    :discrete
    (let [{list :variable/list}   (d/pull @@vms-conn '[{:variable/list [* {:list/options [*]}]}] (:db/id variable))
          {options :list/options} list
          options                 (index-by :list-option/value options)]
      (fn discrete-fmt [value]
        (if-let [option (get options value)]
          (:list-option/name option)
          value)))

    :text
    identity))

(rf/reg-sub
 :worksheet/result-table-formatters
 (fn [_ [_ gv-uuids]]
   (let [results (d/q '[:find ?gv-uuid (pull ?v [*])
                        :in $ % [?gv-uuid ...]
                        :where
                        (lookup ?gv-uuid ?gv)
                        (group-variable _ ?gv ?v)]
                      @@vms-conn rules gv-uuids)]
     (into {} (map
               (fn [[gv-uuid variable]]
                   [gv-uuid (create-formatter variable)])
               results)))))

(rp/reg-sub
 :worksheet/map-units-settings-eid
 (fn [_ [_ ws-uuid]]
   {:type      :query
    :query     '[:find  ?m .
                 :in    $ ?ws-uuid
                 :where
                 [?w :worksheet/uuid ?ws-uuid]
                 [?w :worksheet/table-settings ?t]
                 [?t :table-settings/map-units-settings ?m]]
    :variables [ws-uuid]}))

(rf/reg-sub
 :worksheet/map-units-settings-entity
 (fn [[_ ws-uuid]]
   (rf/subscribe [:worksheet/map-units-settings-eid ws-uuid]))
 (fn [map-units-settings-eid _]
   (re-entity-from-eid map-units-settings-eid)))

(rp/reg-sub
 :worksheet/map-units-enabled?
 (fn [_ [_ ws-uuid]]
   {:type      :query
    :query     '[:find  ?enabled .
                 :in    $ ?ws-uuid
                 :where
                 [?w :worksheet/uuid ?ws-uuid]
                 [?w :worksheet/table-settings ?t]
                 [?t :table-settings/map-units-settings ?m]
                 [?m :map-units-settings/enabled? ?enabled]]
    :variables [ws-uuid]}))

(rp/reg-sub
 :worksheet/result-table-cell-data
 (fn [_ [_ ws-uuid]]
   {:type  :query
    :query '[:find ?row ?col-uuid ?repeat-id ?value
             :in $ ?ws-uuid
             :where
             [?w :worksheet/uuid ?ws-uuid]
             [?w :worksheet/result-table ?rt]
             [?rt :result-table/rows ?r]

             ;;get row
             [?r :result-row/id ?row]

             ;;get-header
             [?r :result-row/cells ?c]
             [?c :result-cell/header ?h]
             [?h :result-header/group-variable-uuid ?col-uuid]
             [?h :result-header/repeat-id ?repeat-id]

             ;;get value
             [?c :result-cell/value ?value]]
    :variables [ws-uuid]}))

(rf/reg-sub
 :worksheet/output-uuid->result-min-values
 (fn [[_ ws-uuid]]
   [(rf/subscribe [:worksheet/result-table-cell-data ws-uuid])
    (rf/subscribe [:worksheet/all-output-uuids ws-uuid])])
 (fn [[result-table-cell-data all-output-uuids] _]
   (reduce
    (fn [acc [_row-id gv-uuid _repeat-id value]]
      (if (contains? (set all-output-uuids) gv-uuid )
        (update acc gv-uuid (fn [min-v]
                              (let [min-float   (js/parseFloat min-v)
                                    value-float (js/parseFloat value)]
                                (min (or min-float ##Inf) value-float))))
        acc))
    {}
    result-table-cell-data)))

(rf/reg-sub
 :worksheet/output-min+max-values
 (fn [[_ ws-uuid]]
   [(rf/subscribe [:worksheet/result-table-cell-data ws-uuid])
    (rf/subscribe [:worksheet/all-output-uuids ws-uuid])])
 (fn [[result-table-cell-data all-output-uuids] _]
   (reduce
    (fn [acc [_row-id gv-uuid _repeat-id value]]
      (if (contains? (set all-output-uuids) gv-uuid )
        (update acc gv-uuid (fn [[min-v max-v]]
                              (let [min-float   (js/parseFloat min-v)
                                    max-float   (js/parseFloat max-v)
                                    value-float (js/parseFloat value)]
                                [(min (or min-float ##Inf) value-float)
                                 (max (or max-float ##-Inf) value-float)])))
        acc))
    {}
    result-table-cell-data)))

(rf/reg-sub
 :worksheet/output-uuid->result-max-values
 (fn [[_ ws-uuid]]
   [(rf/subscribe [:worksheet/result-table-cell-data ws-uuid])
    (rf/subscribe [:worksheet/all-output-uuids ws-uuid])])
 (fn [[result-table-cell-data all-output-uuids] _]
   (reduce
    (fn [acc [_row-id gv-uuid _repeat-id value]]
      (if (contains? (set all-output-uuids) gv-uuid )
        (update acc gv-uuid (fn [max-v]
                              (let [max-float   (js/parseFloat max-v)
                                    value-float (js/parseFloat value)]
                                (max (or max-float ##-Inf) value-float))))
        acc))
    {}
    result-table-cell-data)))

;; returns headers of table in sorted order
(rf/reg-sub
 :worksheet/result-table-headers-sorted
 (fn [_]
   (rf/subscribe [:vms/group-variable-order]))
 (fn [gv-order [_ ws-uuid]]
   (let [headers @(rf/subscribe [:query
                                 '[:find ?gv-uuid ?repeat-id ?units
                                   :in $ ?ws-uuid
                                   :where
                                   [?w :worksheet/uuid ?ws-uuid]
                                   [?w :worksheet/result-table ?r]
                                   [?r :result-table/headers ?h]
                                   [?h :result-header/repeat-id ?repeat-id]
                                   [?h :result-header/group-variable-uuid ?gv-uuid]
                                   [?h :result-header/units ?units]]
                                 [ws-uuid]])]
     (->> headers
          (sort-by (juxt #(.indexOf gv-order (first %))
                         #(second %)))))))

;; returns a map of group-variable uuid to units
(rf/reg-sub
 :worksheet/result-table-units
 (fn [[_ ws-uuid]]
   (rf/subscribe [:worksheet/result-table-headers-sorted ws-uuid]))

 (fn [headers _]
   (into {} (map (juxt first last) headers))))

(rf/reg-sub
 :worksheet/graph-settings
 (fn [[_ ws-uuid]]
   (rf/subscribe [:query '[:find ?gs .
                           :in $ ?ws-uuid
                           :where
                           [?w :worksheet/uuid ?ws-uuid]
                           [?w :worksheet/graph-settings ?gs]]
                  [ws-uuid]]))
 (fn [id _]
   (d/entity @@s/conn id)))

(rf/reg-sub
 :worksheet/all-inputs-entered?
 (fn [_ [_ ws-uuid module-id submodule]]
   true
   #_(let [submodule                             @(rf/subscribe [:wizard/*submodule module-id submodule :input])
         groups                                @(rf/subscribe [:wizard/groups (:db/id submodule)])
         groups-repeat                         (filter #(true? (:group/repeat? %)) groups)
         groups-not-repeat                     (remove #(true? (:group/repeat? %)) groups)
         all-inputs                            @(rf/subscribe [:worksheet/all-inputs ws-uuid])
         groups-not-repeat-all-values-entered? (->> (for [group    groups-not-repeat
                                                          variable (:group/group-variables group)
                                                          :let     [group-uuid (:bp/uuid group)
                                                                    var-uuid   (:bp/uuid variable)]]
                                                      (get-in all-inputs [group-uuid 0 var-uuid]))
                                                    (every? seq))
         groups-repeat-all-values-entered?     (every? (fn [group]
                                                         (let [group-uuid   (:bp/uuid group)
                                                               vars-needed  (* (count (:group/group-variables group))
                                                                               (count @(rf/subscribe [:worksheet/group-repeat-ids ws-uuid group-uuid])))
                                                               vars-entered (reduce (fn [acc [_repeat-id variables]]
                                                                                      (+ acc (count (filter (fn has-value? [[_variable-id val]]
                                                                                                              (seq val))
                                                                                                            variables))))
                                                                                    0
                                                                                    (get all-inputs group-uuid))]
                                                           (= vars-needed vars-entered)))
                                                       groups-repeat)]

     (and groups-not-repeat-all-values-entered? groups-repeat-all-values-entered?))))

(rf/reg-sub
 :worksheet/some-outputs-entered?
 (fn [[_ ws-uuid]]
   (rf/subscribe [:worksheet/all-output-uuids ws-uuid]))

 (fn [all-output-uuids [_ _ws-uuid module-id submodule-slug]]
   (if (seq all-output-uuids)
     (let [submodule       @(rf/subscribe [:wizard/*submodule module-id submodule-slug :output])
           groups          (:submodule/groups submodule)
           group-variables (set (flatten (map #(map :bp/uuid (:group/group-variables %)) groups)))]
       (boolean (seq (set/intersection (set all-output-uuids) group-variables))))
     false)))

(rf/reg-sub
 :worksheet/first-output-submodule-slug
 (fn [_]
   (rf/subscribe [:vms/pull-with-attr :module/name]))
 (fn [modules [_ module]]
   (when module
     (let [module     (first (filter (fn [{m-name :module/name}]
                                   (= (->str module) (str/lower-case m-name))) modules))
           submodules @(rf/subscribe [:vms/pull-children :module/submodules (:db/id module)])]
       (as-> submodules $
         (filter #(= :output (:submodule/io %)) $)
         (sort-by :submodule/order $)
         (first $)
         (:submodule/name $)
         (->kebab $))))))

(rp/reg-sub
 :worksheet/input-gv-uuid+value+units
 (fn [_ [_ ws-uuid row-id]]
   {:type      :query
    :query     '[:find  ?gv-uuid ?value ?units
                 :in $ ?ws-uuid ?row-id
                 :where
                 [?ws :worksheet/uuid ?ws-uuid]
                 [?ws :worksheet/input-groups ?ig]
                 [?ws :worksheet/result-table ?t]
                 [?t  :result-table/rows ?rr]
                 [?rr :result-row/id ?row-id]
                 [?rr :result-row/cells ?c]

                 ;; Filter only input variables
                 [?ig :input-group/inputs ?i]
                 [?i  :input/group-variable-uuid ?gv-uuid]

                 ;; Get  gv-uuid, value and units
                 [?rh :result-header/group-variable-uuid ?gv-uuid]
                 [?rh :result-header/units ?units]
                 [?c  :result-cell/header ?rh]
                 [?c  :result-cell/value ?value]]
    :variables [ws-uuid row-id]}))

(rp/reg-sub
 :worksheet/output-gv-uuid+value+units
 (fn [_ [_ ws-uuid row-id]]
   {:type      :query
    :query     '[:find  ?gv-uuid ?value ?units
                 :in $ ?ws-uuid ?row-id
                 :where
                 [?ws :worksheet/uuid ?ws-uuid]
                 [?ws :worksheet/outputs ?o]
                 [?ws :worksheet/result-table ?t]
                 [?t  :result-table/rows ?rr]
                 [?rr :result-row/id ?row-id]
                 [?rr :result-row/cells ?c]

                 ;; Filter only output variables
                 [?o  :output/group-variable-uuid  ?gv-uuid]

                 ;; Get  gv-uuid, value and units
                 [?rh :result-header/group-variable-uuid ?gv-uuid]
                 [?rh :result-header/units ?units]
                 [?c  :result-cell/header ?rh]
                 [?c  :result-cell/value ?value]]
    :variables [ws-uuid row-id]}))

(rf/reg-sub
 :worksheet/resolve-enum-value

 (fn [_ [_ variable-eid value]]
   (let [variable                (d/pull @@vms-conn '[{:variable/list [* {:list/options [*]}]}] variable-eid)
         {v-list :variable/list} variable
         {options :list/options} v-list
         options                 (index-by :list-option/value options)]
     (if-let [option (get options value)]
       (:list-option/name option)
       value))))
