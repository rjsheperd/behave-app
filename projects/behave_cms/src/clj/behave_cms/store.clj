(ns behave-cms.store
  (:require [behave.schema.core           :refer [all-schemas]]
            [datahike.api                 :as d]
            [behave.datahike-store.main   :as s]
            [behave.config.interface      :refer [get-config]]
            [behave.datom-utils.interface :refer [safe-deref unwrap]]))

(defn connect! [config & [reset?]]
  (if reset?
    (s/reset-datahike! config all-schemas)
    (s/default-conn config all-schemas #(s/migrate! % all-schemas))))

(defn default-conn []
  (if (nil? @s/conn)
    (connect! (get-config :database :config))
    @s/conn))

(defn get-entity [db {id :db/id}]
  (d/pull (safe-deref db) '[*] id))

(defn create-entity! [db data]
  (let [db (unwrap db)]
    (s/transact db [(assoc data :db/id -1)])))

(defn update-entity! [db data]
  (let [db (unwrap db)]
    (s/transact db [data])))

(defn delete-entity! [db {id :db/id}]
  (let [db (unwrap db)]
    (s/transact db [[:db/retractEntity id]])))
